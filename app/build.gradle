ext {
    SUPPORT_LIBRARY_VERSION = "28.0.0"
    WEARABLE_SUPPORT_LIBRARY_VERSION = "2.3.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    google()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.farmerbb.wearoddsnends"
        minSdkVersion 26
        targetSdkVersion 28

        versionCode 4
        versionName "1.1"

        resConfigs "en"
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Wear Odds & Ends-${defaultConfig.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:wear:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"

    compileOnly "com.google.android.wearable:wearable:$WEARABLE_SUPPORT_LIBRARY_VERSION"
    implementation "com.google.android.support:wearable:$WEARABLE_SUPPORT_LIBRARY_VERSION"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
